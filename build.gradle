import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.3'
    id 'com.github.ben-manes.versions' version '0.40.0'
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.named('jar') {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

group = 'net.juniper.netconf'

ext {
    log4jVersion = '2.17.1'
    lombokVersion = '1.18.22'
    mockitoVersion = '4.2.0'
}

// Define an integration-test suite - see also https://docs.gradle.org/7.3/release-notes.html#declarative-test-suites-in-jvm-projects
sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

task integrationTest(type: Test) {
    mustRunAfter tasks.test
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    inputs.dir file('src/integration-test')
    outputs.dir file('build/test-results/integrationTest')
    dependsOn integrationTestClasses
}
check.dependsOn tasks.integrationTest

tasks.withType(Test) {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

checkstyle {
    toolVersion '9.1'
    maxWarnings 0
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

spotbugs {
    effort = Effort.MAX
    reportLevel = Confidence.LOW
    ignoreFailures.set false
    extraArgs.add "-longBugCodes"
}

tasks.withType(SpotBugsTask) {
    //noinspection GroovyAssignabilityCheck
    reports(({
        text.enabled = true
    }))

    //noinspection GroovyAssignabilityCheck
    task "${it.name}Report" {
        def input = file(reports.getByName("TEXT").outputLocation)
        inputs.file input
        doLast {
            input.readLines().forEach {
                println(it)
            }
        }
    }
    it.finalizedBy "${it.name}Report"
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    //    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'org.apache.sshd:sshd-core:2.8.0'

    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation 'org.xmlunit:xmlunit-assertj:2.8.4'

    // Lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    integrationTestImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.1'
}